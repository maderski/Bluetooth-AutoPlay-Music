apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.google.gms.google-services'

buildscript {
    ext {
        build_config = [
                'compile_sdk': 30,
                'build_tools': '30.0.0',
                'min_sdk': 23,
                'target_sdk': 30,
                'version_code': 91,
                'version_name': '4.0'
        ]

        versions = [
                'kotlin': [
                        'stdlib': kotlin_version,
                        'coroutines_core': '1.3.7'
                ],
                'androidx': [
                        'work_runtime_ktx': '2.3.4',
                        'appcompat': '1.1.0',
                        'legacy_support_v4': '1.0.0',
                        'core_ktx': '1.3.0',
                        'constraintlayout': '1.1.3',
                        'life_cycle': '2.2.0'
                ],
                'google': [
                        'material': '1.1.0',
                        'gson': '2.8.5'
                ],
                'firebase': [
                        'firebase_core': '17.4.3',
                        'firebase_crash': '16.2.1'
                ],
                'third_party': [
                        'sunrise_sunset_calc': '1.2',
                        'koin_android': '2.1.5'
                ],
                'test': [
                        'junit': '4.12'
                ]
        ]

        deps = [
                'kotlin': [
                        'stdlib': "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin.stdlib}",
                        'coroutines_core': "org.jetbrains.kotlinx:kotlinx-coroutines-core:${versions.kotlin.coroutines_core}"
                ],
                'androidx': [
                        'work_runtime_ktx': "androidx.work:work-runtime-ktx:${versions.androidx.work_runtime_ktx}",
                        'appcompat': "androidx.appcompat:appcompat:${versions.androidx.appcompat}",
                        'legacy_support_v4': "androidx.legacy:legacy-support-v4:${versions.androidx.legacy_support_v4}",
                        'core_ktx': "androidx.core:core-ktx:${versions.androidx.core_ktx}",
                        'constraintlayout': "androidx.constraintlayout:constraintlayout:${versions.androidx.constraintlayout}",
                        'lifecycle_viewmodel_ktx': "androidx.lifecycle:lifecycle-viewmodel-ktx:${versions.androidx.life_cycle}",
                        'lifecycle_livedata_ktx': "androidx.lifecycle:lifecycle-livedata-ktx:${versions.androidx.life_cycle}"
                ],
                'google': [
                        'material': "com.google.android.material:material:${versions.google.material}",
                        'gson': "com.google.code.gson:gson:${versions.google.gson}"
                ],
                'firebase': [
                        'firebase_core': "com.google.firebase:firebase-core:${versions.firebase.firebase_core}",
                        'firebase_crash': "com.google.firebase:firebase-crash:${versions.firebase.firebase_crash}"
                ],
                'third_party': [
                       'sunrise_sunset_calc': "com.luckycatlabs:SunriseSunsetCalculator:${versions.third_party.sunrise_sunset_calc}",
                       'koin_android': "org.koin:koin-android:${versions.third_party.koin_android}"
                ],
                'test': [
                        'junit': "junit:junit:${versions.test.junit}"
                ]
        ]
    }
}

android {
    compileSdkVersion build_config.compile_sdk
    buildToolsVersion build_config.build_tools

    defaultConfig {
        applicationId "maderski.bluetoothautoplaymusic"
        minSdkVersion build_config.min_sdk
        targetSdkVersion build_config.target_sdk
        versionCode build_config.version_code
        versionName build_config.version_name
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    // kotlin
    implementation deps.kotlin.stdlib
    implementation deps.kotlin.coroutines_core
    // androidx
    implementation deps.androidx.work_runtime_ktx
    implementation deps.androidx.appcompat
    implementation deps.androidx.legacy_support_v4
    implementation deps.androidx.core_ktx
    implementation deps.androidx.constraintlayout
    implementation deps.androidx.lifecycle_viewmodel_ktx
    implementation deps.androidx.lifecycle_livedata_ktx
    // google
    implementation deps.google.material
    implementation deps.google.gson
    // firebase
    implementation deps.firebase.firebase_core
    implementation deps.firebase.firebase_crash
    // luckycatlabs
    implementation deps.third_party.sunrise_sunset_calc
    // koin
    implementation deps.third_party.koin_android
    // junit
    testImplementation deps.test.junit
}
